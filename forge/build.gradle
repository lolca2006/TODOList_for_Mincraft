plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly project(":common")

    // JEI Forge API
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"
}

jar {
    finalizedBy('reobfJar')
}

// --- Publishing ---

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "UNZyOOB9"
    versionNumber = "${version}"
    versionName = "Todo List Mod ${version} (Forge)"
    versionType = "release"
    uploadFile = tasks.jar
    gameVersions = [minecraft_version]
    loaders = ["forge"]
    dependencies {
        optional.project "jei"
    }
    changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: "none"
    def mainFile = upload("1457776", tasks.jar)
    mainFile.releaseType = "release"
    mainFile.addGameVersion(minecraft_version)
    mainFile.addModLoader("Forge")
    mainFile.changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
    mainFile.changelogType = "text"
}

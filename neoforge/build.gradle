plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

neoForge {
    version = neoforge_version

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add("src/main/resources/META-INF/accesstransformer.cfg")
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    compileOnly project(":common")

    // JEI NeoForge API
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
}

// --- Publishing ---

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "UNZyOOB9"
    versionNumber = "${version}"
    versionName = "Todo List Mod ${version} (NeoForge)"
    versionType = "release"
    uploadFile = tasks.jar
    gameVersions = [minecraft_version]
    loaders = ["neoforge"]
    dependencies {
        optional.project "jei"
    }
    changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: "none"
    def mainFile = upload("1457776", tasks.jar)
    mainFile.releaseType = "release"
    mainFile.addGameVersion(minecraft_version)
    mainFile.addModLoader("NeoForge")
    mainFile.changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
    mainFile.changelogType = "text"
}

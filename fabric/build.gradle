plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
    id 'com.modrinth.minotaur'
    id 'net.darkhax.curseforgegradle'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    // JEI Fabric API
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"
}

loom {
    accessWidenerPath.set(file("src/main/resources/${mod_id}.accesswidener"))
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

// --- Publishing ---

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "UNZyOOB9"
    versionNumber = "${version}"
    versionName = "Todo List Mod ${version} (Fabric)"
    versionType = "release"
    uploadFile = tasks.remapJar
    gameVersions = [minecraft_version]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
        optional.project "jei"
    }
    changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
}

task curseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: "none"
    def mainFile = upload("1457776", tasks.remapJar)
    mainFile.releaseType = "release"
    mainFile.addGameVersion(minecraft_version)
    mainFile.addModLoader("Fabric")
    mainFile.changelog = System.getenv("CHANGELOG") ?: "See GitHub release notes."
    mainFile.changelogType = "text"
}
